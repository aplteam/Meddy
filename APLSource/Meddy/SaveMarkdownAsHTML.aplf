 {r}←{askFlag}SaveMarkdownAsHTML n;filename
 r←⍬
 :If MarkdownHasChanged n
     'Export HTML'TellMarkdownNeedsSaving n
 :Else
     askFlag←{0<⎕NC ⍵:'ASK'≡1 ⎕C⍎⍵ ⋄ 0=≢n.∆HtmlFilename}'askFlag'
     :If 0=≢n.∆HtmlFilename
     :AndIf ~0=≢n.∆Filename
         n.∆HtmlFilename←↑{⍺,⍵}/(¯1↓1 ⎕NPARTS n.∆Filename),⊂'.html'
     :EndIf
     :If askFlag
         filename←AskForSaveHtmlFilename n
     :Else
         filename←n.∆HtmlFilename
     :EndIf
     :If 0≠≢filename
         filename←'expand'F.NormalizePath{(↑,/2↑⍵),{0=≢⍵:'.html' ⋄ ⍵}2⊃⍵}⎕NPARTS filename
         :If IsPresentation n
             SaveMarkdownAsPresentation n filename
         :Else
             SaveMarkdownAsHTML_ n filename
         :EndIf
         n.∆HtmlFilename←filename
         WriteToStatusbar n('HTML saved: ',filename)
     :EndIf
 :EndIf
⍝Done
