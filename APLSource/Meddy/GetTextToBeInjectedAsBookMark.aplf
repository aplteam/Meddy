 text←GetTextToBeInjectedAsBookMark n;parms;success;bool;bool2;depth;items;buff;parms2;linkText;msg
 text←''
 parms←G.SelectionTool.CreateParms
 parms.addToCaption←'Bookmark links'
 parms.info←'Select the header you want to link to:'
 :If IsPresentation n
     bool←n.Toc.Depth=1
     (depth items)←bool∘/¨n.Toc.(Depth Items)
     buff←((2×depth-1)⍴¨' '),¨items
 :Else
     bool←n.Toc.Depth>0
     (depth items)←bool∘/¨n.Toc.(Depth Items)
     buff←((2×depth-1)⍴¨' '),¨items
 :EndIf
 :If 0=≢buff
     msg←'In order to insert a bookmark link at least one header of level 2 or higher is required.'
     {}msg TellNoHeadersYet n
 :Else
     (success bool2)←n.∆globalParms parms G.SelectionTool.Run buff
     :If success
         parms2←G.Request.CreateParmsForString
         parms2.addToCaption←'Link text'
         parms2.default←↑bool2/bool/n.Toc.Items
         parms2.default←{~' '∊⍵:⍵ ⋄ l←(⍵⍳' ')↑⍵ ⋄ 0=+/(∨/'.'∊l)∧×/(l~'.')∊⎕D:⍵ ⋄ (1+≢l)↓⍵}parms2.default
         (success linkText)←n.∆globalParms parms2 G.Request.AskForString'Please specify the link text:'
     :AndIf success
         :If linkText≢parms2.default
             text←'[',parms2.default,'](# "',linkText,'" {style="color: red;"})'
         :Else
             text←'[',linkText,'](# {style="color: red;"})'
         :EndIf
     :EndIf
 :EndIf
⍝Done
