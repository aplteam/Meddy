 {rc}←{statusbarFlag}SaveText(filename text);buff;filename
⍝ In case of success a 0 is returned. In case of an error the non-zero error code is returned.
⍝ In particular error code 19 is of interest ("The media is write protected.", "File owned by another process" etc.)
⍝ because it means that attempting to save the HTML in the same directory won't be successfull either.
⍝ See also SaveHTML.
 rc←0
 statusbarFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'statusbarFlag'
 buff←{↓⍣(↑2=⍴⍴⍵)⊣⍵}text
 :Trap EventCodes.(FILE_ACCESS_ERROR FILE_DAMAGED FILE_NAME_ERROR FILE_SYSTEM_NO_SPACE FILE_TIE_ERROR FILE_TIE_QUOTA_USED_UP FILE_TIED)
     {⍵:.}(∨/'<!DOCTYPE html>'⍷∊3↑buff)   ⍝TODO⍝  Make sure that we do not save HTML accidentally
     {}(⊂buff)F.NPUT filename 1
     :If statusbarFlag
         WriteToStatusbar n('Saved at ',A.FormatDateTime ⎕TS)
     :EndIf
 :Else
     TellFileSaveError filename
     rc←⎕EN
 :EndTrap
