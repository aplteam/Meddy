 filename←{filename}AskForSaveFilename n;parms;path;flag;extension;buff
 parms←G.FileBox.CreateParms
 parms.fileMode←'Write'
 :If 0=⎕NC'filename'
     filename←↑,/1↓⎕NPARTS n.∆Filename
 :Else
     parms.file←↑,/1↓⎕NPARTS filename
 :EndIf
 :If 0=≢filename
 :AndIf 0<≢buff←' {0,3}# [^$]+'⎕S{⍵.Match}⍠('Mode' 'L')⊣n.Markdown.Text
 :AndIf 0<≢buff←↑buff
     filename←{⍵↓⍨2+1⍳⍨'# '⍷⍵}buff
 :EndIf
 :If 0=≢path←↑⎕NPARTS filename
     path←n.∆Path
 :EndIf
 flag←0
 :Repeat
     parms.addToCaption←'Save Markdown file'
     parms.filters←⊂'*.md' 'Markdown file'
     parms.file←filename
     :If 0=≢filename←n.∆globalParms parms G.FileBox.Run path
         :Return
     :EndIf
     extension←↑¯1↑⎕NPARTS filename
     :If (⊂A.Lowercase extension)∊'.md' '.markdown'
         flag←1
     :ElseIf 0=≢extension
         filename,←'.md'
         flag←1
     :Else
         {}TellExtensionIsInvalid n
     :EndIf
 :Until flag
⍝Done
