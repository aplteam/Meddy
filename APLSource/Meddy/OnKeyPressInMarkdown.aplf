 {r}←n OnKeyPressInMarkdown msg;hasFocus;row;key;cl;buff
 r←1
 key←,2⊃msg
 hasFocus←2 ⎕NQ #'GetFocusObj'
 :If 3<≢msg
     :If 9 1≡msg[4 5]               ⍝ Shift+Tab?!
         n.Markdown.SelText←2⍴n.Markdown.SelText[1]
         ⍝{}1 ⎕NQ n.Toc'ItemUp'(n.Toc.SelItems⍳1)0 0 8
         0 ScrollToInMarkdown n(n.Toc.SelItems⍳1)
         {}⎕NQ n.Toc'GotFocus'
         n.(Row Col).Text←⊂''
         r←0
         :Return
     :ElseIf 107 2≡msg[4 5]         ⍝ Ctrl++?!
         n ChangeFontSize 1
     :ElseIf 109 2≡msg[4 5]         ⍝ Ctrl+-?!
         n ChangeFontSize ¯1
     :ElseIf 2=≢key
         :Select key
         :Case 'HT'
             HandleTabKey ⍬
         :Case 'CP'
             :If n.HTML≡hasFocus
                 cl←⎕NEW⊂'Clipboard'
                 cl.Text←n.HTML.Text
             :EndIf
         :Case 'PT'
             PolishClipboard ⍬
         :EndSelect
     :ElseIf 0=≢key
         r←4=msg[5]  ⍝ "Alt" key?!
     :EndIf
     n.StatusField1.Text←''
     n.StatusField1.BCol←0
 :EndIf
 :If 3=≢msg
 :OrIf 4≠5⊃msg                    ⍝ Ony when no ALT key is pressed
     :Trap 0 ⋄ ⎕NQ hasFocus'GotFocus' ⋄ :EndTrap
     ⎕NQ n.∆Form 9999           ⍝ Sets Row and Col in Statusbar, preserve data for undo/redo
 :EndIf
⍝Done
