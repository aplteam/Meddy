 GetFolderFromMeddyConfigFile←{
⍝ Tries to find a Meddy config file (.Meddy) in the folder the markdown document was loaded from.
⍝ If there is such a file it looks for `outputpath` and returns its value, otherwise ''.
     n←⍵
     (folder filename extension)←⎕NPARTS n.myFile.filenameMarkdown
     0=FilesAndDirs.IsFile folder,'.meddy':''
     data←⊆↑F.NGET(↑⎕NPARTS n.myFile.filenameMarkdown),'.meddy'
     data←⊃'='A.Split¨,data
     bool←(' '~¨⍨⎕C data[;0])≡¨⊂'outputpath'
     0=+/bool:''
     target←1↓¯1↓⎕TC~⍨A.DLB A.DTB↑bool/data[;1]
     {(':'∊⍵)∨('\\'≡2⍴⍵)∨('/'=1⍴⍵)}target:'expand'FilesAndDirs.NormalizePath target,'/',filename,'.html'           ⍝ Absolute path? Done!
     'expand'FilesAndDirs.NormalizePath folder,target,filename,'.html'
 }
