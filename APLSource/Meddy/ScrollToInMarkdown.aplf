 {r}←{selectText}ScrollToInMarkdown(n ind);length;row;caption;pattern;level;offset;buff;best;header
 r←⍬
 selectText←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'selectText'
 ind←↑ind
 :If ind∊⍳≢n.Toc.∆Toc
     (header row)←n.Toc.∆Toc[ind;1 3]
     :If row≥≢n.Markdown.Text
         :Return ⍝ Yes, this did happen!
     :EndIf
     caption←A.DLB A.DTB{⍵↓⍨+/∧\'#'=⍵}(row-1)⊃n.Markdown.Text
     :If '}'=¯1↑caption
     :AndIf '{'∊caption
         caption←A.DTB{⍵↑⍨(≢⍵)-1+(⌽⍵)⍳'{'}caption
     :EndIf
     :If header≢caption
         2 RefreshPreview n
         :If ind≥≢n.Toc.∆Toc
             :Return
         :EndIf
         (header row)←n.Toc.∆Toc[ind;1 3]
     :EndIf
     length←1+≢(row-1)⊃n.Markdown.Text
     :If selectText
         n.Markdown.SelText←buff←((row),0)((row),length)
     :EndIf
     ScrollToInMarkdown_ n.Markdown
     ⎕DL 0.1
     :If selectText
         n.Markdown.SelText←buff
     :EndIf
     n.(Row Col).Text←⍕¨1⊃n.Markdown.SelText
 :EndIf
⍝Done
