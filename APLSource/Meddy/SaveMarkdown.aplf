 {r}←SaveMarkdown n;parms;htmlFilename;flag;embeddedParms;bool;saveHTML;buff
 r←⍬
 :If 0=≢n.∆Filename
     SaveMarkdownAs n
     saveHTML←n.∆parms.saveHtml
 :Else
     SaveText n.∆Filename n.Markdown.Text
     n.Markdown.∆LastSavedVersion←n.Markdown.Text
     RefreshPreview n
     embeddedParms←↑GetEmbeddedParms n.Markdown.Text
     :If 0<+/bool←embeddedParms{((≢⍵)↑¨⍺)≡¨⊂⍵}'[parm]:saveHTML'
         saveHTML←(↑¯1↑⍸bool)⊃embeddedParms
         saveHTML←{↑↑(//)⎕VFI ⍵↓⍨1+⍵⍳'='}saveHTML
     :ElseIf 0<n.∆parms.⎕NC'embeddedParms'
     :AndIf ∨/n.∆parms.embeddedParms{((≢⍵)↑¨⍺)≡¨⊂⍵}'[parm]:saveHTML'
         saveHTML←SaveHTMLValues.YesButKeepAsking
     :Else
         saveHTML←n.∆parms.saveHtml
     :EndIf
     n.∆parms.embeddedParms←embeddedParms
     :If ~saveHTML∊SaveHTMLValues.(Always Never)
         :If n.∆parms.firstFlag
             n.∆parms.firstFlag←0
             saveHTML←DoYouWantToSaveHtml n
         :Else
             :If saveHTML∊SaveHTMLValues.(NoButKeepAsking YesButKeepAsking)
                 saveHTML←saveHTML DoYouWantToSaveHtml n
             :EndIf
         :EndIf
     :EndIf
 :EndIf
 n.∆parms.saveHtml←saveHTML
 flag←saveHTML∊SaveHTMLValues.(Always YesButKeepAsking)
 :If flag
     :If 0=≢n.∆HtmlFilename
         htmlFilename←(↑,/¯1↓⎕NPARTS n.∆Filename),'.html'
     :Else
         htmlFilename←((-{1+'.'⍳⍨⌽⍵}n.∆HtmlFilename)↓n.∆HtmlFilename),'.html'   ⍝ Make sure extension is ".html"
     :EndIf
     n.∆HtmlFilename←htmlFilename
     :If 0<≢buff←GetFolderFromMeddyConfigFile n
         n.∆HtmlFilename←buff
     :EndIf
     :If IsPresentation n
         SaveMarkdownAsPresentation n n.∆HtmlFilename
     :Else
         1 SaveText htmlFilename n.HTML.∆HTML
     :EndIf
 :EndIf
