 {r}←SaveMarkdown n;parms;htmlFilename;flag;embeddedParms;bool;saveHTML;buff
 r←⍬
 :If n.myFile.firstFlag
     SaveMarkdownAs n
 :Else
     SaveText n.myFile.filenameMarkdown n.Markdown.Text
     n.Markdown.∆LastSavedVersion←n.Markdown.Text
     RefreshPreview n
     :If IsPresentation n
         SaveMarkdownAsPresentation n n.myFile.filenameHtml
     :Else
         embeddedParms←↑GetEmbeddedParms n.Markdown.Text
         :If 0<+/bool←embeddedParms{((≢⍵)↑¨⍺)≡¨⊂⍵}'[parm]:saveHTML'
             saveHTML←(↑¯1↑⍸bool)⊃embeddedParms
             saveHTML←1={↑↑(//)⎕VFI ⍵↓⍨1+⍵⍳'='}saveHTML
         :ElseIf 0<n.∆parms.⎕NC'embeddedParms'
         :AndIf ∨/n.∆parms.embeddedParms{((≢⍵)↑¨⍺)≡¨⊂⍵}'[parm]:saveHTML'
             saveHTML←0
         :Else
             saveHTML←n.myFile.saveHtml
         :EndIf
         n.∆parms.embeddedParms←embeddedParms
         :If 1=saveHTML
             :Trap 16 19
                 (⊂,n.HTML.∆HTML)F.NPUT n.myFile.filenameHtml 1
             :Else
                 TellFileSaveError n.myFile.filenameHtml
             :EndTrap
         :EndIf
     :EndIf
 :EndIf
⍝Done
