 {r}←{tellNothingFlag}ReportConversionProblem n;parms;msg;report;⎕TRAP
 r←⍬
 tellNothingFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'tellNothingFlag'
 :If 0=n.⎕NC'∆Report'
     {}TellNothingToReport⍣tellNothingFlag⊣n
 :Else
     report←n.∆Report
     ⎕TRAP←0 'S'
     :If 0=n.∆Prefs.Warnings
     :OrIf n.∆INI{~⍺.Exist ⍵:0 ⋄ ↑⍺.Get ⍵}'CONFIG:warnings'
         report/⍨←'Warning:'{⍺∘≢¨(≢⍺)↑¨⍵}report
     :EndIf
     :If 0=≢report
         {}TellNothingToReport⍣tellNothingFlag⊣n
     :Else
         #.CursorObj←0
         parms←G.Dialogs.CreateParmsForShowLongMsg
         parms.addToCaption←'Warnings & error msg'
         parms.buttons←,⊂'OK'
         msg←'Markdown-to-HTML warnings and error messages'
         msg←msg((≢msg)⍴'=')
         msg,←report
         n.∆Form.CursorObj←0
         n.∆globalParms parms G.Dialogs.ShowLongMsg msg
     :EndIf
 :EndIf
