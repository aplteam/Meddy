 {r}←{tellNothingFlag}ReportConversionProblem n;parms;msg;report
 r←⍬
 tellNothingFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'tellNothingFlag'
 :If 0=n.⎕NC'∆Report'
     {}TellNothingToReport⍣tellNothingFlag⊣n
 :Else
     :If n.∆Menubar.noWarnings.Checked
         1 WriteToStatusbar n''
         report←n.∆Report
         :If 0=n.∆Prefs.Warnings
         :OrIf n.∆INI{~⍺.Exist ⍵:0 ⋄ ↑⍺.Get ⍵}'CONFIG:warnings'
             report/⍨←'Warning:'{⍺∘≢¨(≢⍺)↑¨⍵}report
         :EndIf
         :If 0=≢report
             {}TellNothingToReport⍣tellNothingFlag⊣n
         :Else
             ShowWarnings_ n report
         :EndIf
     :Else
         1 WriteToStatusbar n'There are warnings'
     :EndIf
 :EndIf
