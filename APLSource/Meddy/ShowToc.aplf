 n←{row}ShowToc n;toc;depth;diff;invalid;states;allStates;was;now;closingTags;b
 row←n.Toc{0<⎕NC ⍵:⍎⍵ ⋄ 0}'row'
 toc←⊃n.HTML.∆ns.toc
 n.Toc.Visible←0
 :If 0=≢toc
     n.Toc.Items←''
     n.Toc.SelItems←⍬
     n.Toc.Depth←⍬
     n.Toc.∆Toc←0 4⍴⍬
 :Else
     toc[;1]←toc[;1]~¨'`'
     n.Toc.Items←toc[;1]
     closingTags←{'(</[^?]+>)'⎕S{⍵.Match}⊣⍵}¨n.Toc.Items
     :If ∨/b←≢¨closingTags
         (b/n.Toc.Items)←(b/closingTags){⍺ ⎕R''⊣⍵}¨b/n.Toc.Items
         (b/n.Toc.Items)←('/'~¨¨⍨b/closingTags){⍺ ⎕R''⊣⍵}¨b/n.Toc.Items
     :EndIf
     :Trap 11
         n.Toc.Depth←0⌈¯1+1,2{(⍺+1)≥⍵:⍵ ⋄ ⍺+1}/toc[;0] ⍝ Correct any user mistakes, otherwise we get a DOMAIN ERROR.
     :Else
         n.Toc.Depth←(≢toc)⍴0    ⍝ Last resort
     :EndTrap
     allStates←n.Toc GetState¨⍳≢n.Toc.Items
     n.Toc.Depth←n.Toc.((⊃⌈/≢¨(Depth Items))↑Depth)
     was←n.Toc.(↑,[0.5]/Depth Items)
     n.Toc.RealDepth←toc[;0]        ⍝ In case the user has built an invalid structure of headers we need to remember his definition
     toc[;0]←toc[;0]-(↑toc[;0])-1
     n.TocSF.(Active Visible)←1
     {}{2 ⎕NQ n.Toc'Expanding'⍵}¨⍳≢n.Toc.Depth   ⍝ ⍸ n.Toc.Depth∊0 1
     toc[;1]←(,¨'“”')⎕R(,¨'""')⊣toc[;1]
     n.Toc.∆Toc←toc
     :If 0≠≢invalid←⍸0,2{(⍺+1)≥⍵:0 ⋄ 1}/toc[;0]
         states←n.Toc GetState¨invalid
         (4⊃¨states)←0                  ⍝ Reset
         :If ~0=≢invalid
             (4⊃¨states)←1              ⍝ Make invalid bold
         :EndIf
         {}n.Toc SetState¨invalid,¨⊂¨states
         WriteToStatusbar n'There is a problem with the menu hierarchy'
         n.StatusField1.FCol←255 0 0
     :EndIf
     n.Toc.SelItems[row⌊¯1+≢n.Toc.SelItems]←1
 :EndIf
 n.Toc.Visible←1
 SyncWithTocAndPreview n
⍝Done
