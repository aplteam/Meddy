 carryOnFlag←{caption}CheckForChanges n;parms;msg;answer;filename;htmlFilename;caption
⍝ `carryOnFlag` gets 1 in case the user cancels the whole operation
 caption←{0=⎕NC ⍵:'Close document' ⋄ ⍎⍵}'caption'
 carryOnFlag←0
 :If MarkdownHasChanged n
     parms←G.Dialogs.CreateParmsForYesNoCancel
     parms.addToCaption←caption
     parms.(hasStatusbar hasProgressbar)←0
     msg←'Would you like to save the latest changes first?'
     answer←n.∆globalParms parms G.Dialogs.YesNoCancel msg
     :If 1=answer
         :If 0=≢n.∆Filename
             :If 0=≢filename←AskForSaveFilename n
                 :Return
             :Else
                 UpdateFilename n filename
             :EndIf
         :EndIf
         SaveText n.∆Filename n.Markdown.Text
         :If n.∆parms.saveHtml∊SaveHTMLValues.(Always YesButKeepAsking)
             RefreshPreview n
             htmlFilename←(↑,/¯1↓⎕NPARTS n.∆Filename),'.html'
             A.WriteUtf8File htmlFilename n.HTML.Text
         :EndIf
     :EndIf
     carryOnFlag←¯1=answer
 :EndIf
