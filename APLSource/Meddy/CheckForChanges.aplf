 carryOnFlag←{caption}CheckForChanges n;parms;msg;answer;filename;htmlFilename;caption
⍝ `carryOnFlag` gets 1 in case the user cancels the whole operation
 caption←{0=⎕NC ⍵:'Close document' ⋄ ⍎⍵}'caption'
 carryOnFlag←0
 :If MarkdownHasChanged n
     parms←G.Dialogs.CreateParmsForYesNoCancel
     parms.addToCaption←caption
     parms.(hasStatusbar hasProgressbar)←0
     msg←'Would you like to save the latest changes first?'
     answer←n.∆globalParms parms G.Dialogs.YesNoCancel msg
     :If 1=answer
         :If n.myFile.isTempFile
             :If 0=≢filename←AskForSaveFilename n
                 carryOnFlag←1
                 :Return
             :Else
                 UpdateFilename n filename
             :EndIf
         :EndIf
         carryOnFlag←0≠SaveText n.myFile.filenameMarkdown n.Markdown.Text
         :If n.myFile.saveHtml
             SaveHTML n
         :EndIf
     :Else
         carryOnFlag←answer=¯1
     :EndIf
 :EndIf
