 n←Init n;msg;parms
 {}2035⌶'ScaledPixel'
 EstablishRefs ⍬
 :If A.IsDevelopment
     F.PolishCurrentDir
 :Else
     {}F.Cd↑1 ⎕NPARTS 1↓{⍵↑⍨+/∧\1=+\'"'=⍵}2 ⎕NQ #'GetCommandLine'
 :EndIf
 n.∆globalParms←G.APLGuiGlobalParms.CreateDefaults
 n.∆RecentFiles←''
 n.∆Find←⎕NS''
 n.∆Find.(SearchFor ReplaceBy)←⊂''
 n.∆Find.(IsRegEx Case IgnoreInlineCode IgnoreCodeBlocks)←0
 n.∆AllFontSizes←10 11 12 13 15 16 17 18 20 21 22 23 25 26 28 29 30 31 32 34 35 37 38 39 40
 n←0 Init2 n
 n.∆globalParms.caption←'Meddy'
 n.∆globalParms.globalRegKey←'HKLM\SOFTWARE\APL Team Ltd\Meddy'
 n.∆globalParms.userRegKey←'HKCU\SOFTWARE\APL Team Ltd\Meddy'
 n←EstablishPreferences n
 n.∆CurrentFontSize←{0::17 ⋄ ↑(//)⎕VFI ⍵}'17'##.WinRegSimple.Read n.∆globalParms.regKey
⍝Done
