 CheckForNewVersion←{
     ⍝ Check if a new version is available.
     ⍝ ⍵ [1] = project URL, for example 'api.github.com/repos/aplteam/fire/releases/latest'
     ⍝ ⍵ [2] = Version, for eaample '3.4.6'
     ⍝ ← [1] = New version number or ⍬'
     ⍝ ← [2] = Message'
     ⍝   [3] = URL to new version.

     ⍝ Regular api requests are limited to 60 calls per hour from the same IP!
     gitPath←'https://',⎕IO⊃⍵
     version←(1+⎕IO)⊃⍵
     msg←'Version Status: '
     0::⍬((msg,'Error Checking')({⍵↓⍨2+⍵⍳':'}{⍵↑⍨1⍳⍨⎕TC[1 2]⍷⍵}⍕⎕EXCEPTION))''
     fixVersion←{100 100 1000⊥⍵}
     currentVersion←fixVersion 3↑(⎕IO+1)⊃'.'⎕VFI version
     ⎕USING←'System.Net,system.dll' 'System.IO' 'System.Text'
     currentProtocl←ServicePointManager.SecurityProtocol
     ServicePointManager.SecurityProtocol←SecurityProtocolType.Tls12
     req←WebRequest.CreateHttp⊂gitPath
     req.UserAgent←'FiRe APL Client'  ⍝ required for github api
     res←req.GetResponse
     ServicePointManager.SecurityProtocol←currentProtocl
     res.StatusCode≠res.StatusCode.OK:⍬(msg,'Error Checking')''
     textStream←⎕NEW StreamReader res.GetResponseStream
     jsondata←textStream.ReadToEnd
     data←⎕JSON jsondata
     gitVersion←(1+⎕IO)⊃'.'⎕VFI(data.tag_name∊⎕D,'.')/data.tag_name
     gitVersion←fixVersion gitVersion
     ~gitVersion>currentVersion:⍬(msg,'Current')''
     data.tag_name(msg,data.tag_name,' available')data.html_url
 }
