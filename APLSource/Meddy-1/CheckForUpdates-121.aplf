 {r}←CheckForUpdates n;tag;msg;dmx;api;flag;url
 r←⍬
 api←⎕NEW ##.GitHubAPIv3(,⊂'aplteam')
 :Trap 0
     tag←api.GetLatestTag'Meddy'
 :Else
     n TellErrorWhileCheckingForBetterVersion↑⎕DMX.DM
     :Return
 :EndTrap
 flag←api.CheckForUpdate'Meddy'(1⊃Version)
 :If ~flag
     TellMeddyIsUp2Date n
 :Else
     msg←⊂'There is a better version available on GitHub.'
     msg,←'' 'Would you like to download this version?'
     :If n YesOrNo msg
         :Trap 0
             url←api.GetRelease'Meddy' 'EXE'
         :Else
             dmx←⎕DMX
             n TellErrorWhileCheckingForBetterVersion{0=≢⍵:'' ⋄ ↑⍵}dmx.DM
             :Return
         :EndTrap
         :If 0=≢url
             n TellErrorWhileCheckingForBetterVersion''
         :Else
             ##.APLTreeUtils.GoToWebPage url
         :EndIf
     :EndIf
 :EndIf
