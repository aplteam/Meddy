 {r}←SaveMarkdown n;parms;htmlFilename;flag;embeddedParms;bool;saveHTML
 r←⍬
 :If 0∊≢n.∆Filename
     SaveMarkdownAs n
 :Else
     SaveText n.∆Filename n.Markdown.Text
     n.Markdown.∆LastSavedVersion←n.Markdown.Text
     RefreshPreview n
     embeddedParms←↑GetEmbeddedParms n.Markdown.Text
     :If 0<+/bool←embeddedParms{((⍴⍵)↑¨⍺)≡¨⊂⍵}'[parm]:saveHTML'
         saveHTML←(↑¯1↑⍸bool)⊃embeddedParms
         saveHTML←{↑(//)⎕VFI ⍵↓⍨1+⍵⍳'='}saveHTML
         :If ~saveHTML∊¯1 0 1 2
             saveHTML←⍬
         :EndIf
     :Else
         saveHTML←⍬
     :EndIf
     :If (,saveHTML)≡,¯1      ⍝ ¯1 means "undefined"
     :OrIf saveHTML≡⍬
         :If 0=flag←1=n.∆parms.saveHtml
             flag←DoYouWantToSaveHtml n
         :EndIf
     :Else
         :If 2=saveHTML
             flag←saveHTML DoYouWantToSaveHtml n
         :Else
             flag←saveHTML
         :EndIf
     :EndIf
     :If flag
         :If 0∊⍴n.∆HtmlFilename
             htmlFilename←(↑,/¯1↓⎕NPARTS n.∆Filename),'.html'
         :Else
             htmlFilename←n.∆HtmlFilename
         :EndIf
         n.∆HtmlFilename←htmlFilename
         :If IsPresentation n
             SaveMarkdownAsPresentation n n.∆HtmlFilename
         :Else
             1 SaveText htmlFilename n.HTML.∆HTML
         :EndIf
     :EndIf
 :EndIf
