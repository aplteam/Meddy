 GetEmbeddedParmsAndRemoveFromMarkdown←{
 ⍝ Returns both the markdown with the embedded parms and the embedded parms as such.
 ⍝ Ignores comment lines between any lines that carry `[parm]:` and also
 ⍝ the LeanPub definition of the encoding (`{:: encoding="`).
     markdown←⍵
     bool←~WhereIsLeanPubEncodingLine markdown
     0=+/isParm←bool\∧\'['=↑¨bool/markdown:markdown''
     embeddedParms←isParm/markdown
     pattern←'(^\s{0,3}\[parm]:\s{0}\b.*\b\s{0,}=).*$'
     ind←pattern ⎕S 2⍠('Greedy' 0)('IC' 1)⊣embeddedParms
     0=≢embeddedParms←embeddedParms[ind]:markdown''
     embeddedParms←(⍴'[parm]:')↓¨embeddedParms
     embeddedParms←SplitEmbeddedParm¨embeddedParms
     ((~bool∧~isParm)/markdown)←⊂''
     markdown embeddedParms
 }
