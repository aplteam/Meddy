 R←Test_Misc_004(stopFlag batchFlag);⎕TRAP;CreateFolderAndMarkdownFile;folder;msg;n;targetFolder
⍝ Test handling of .meddy config file
⍝ The test case creates a new document in a temp folder in order to make sure no side effects take place.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 CreateFolderAndMarkdownFile←{
     folder←##.FilesAndDirs.GetTempPath,↑1↓⎕SI
     _←'Create!'##.FilesAndDirs.CheckPath folder,'/level2'
     _←(⊂⍵)⎕NPUT(folder,'/level2/my.md')1
     _←(⊂⍺)⎕NPUT(folder,'/level2/.meddy')1
     folder
 }

 msg←''
 msg,←⊂'[parm]:saveHTML = 1'
 msg,←⊂''
 msg,←⊂'Make a change and then issue the "Save" command from the "File" menu,'
 msg,←⊂'then select the "Quit" command from the "File" menu.'
 folder←'outputpath=''../'''CreateFolderAndMarkdownFile msg
 n←∆RunMeddy folder,'/level2/my.md'
 n.Markdown.Text,←⊂'Changed at ',##.APLTreeUtils2.FormatDateTime ⎕TS
 n.∆parms.firstFlag←0
 1 ⎕NQ n.∆Menubar.save'Select'
 →T.GoToTidyUp 1≠≢↑##.FilesAndDirs.Dir folder,'\*.html'
 1 ⎕NQ n.∆Menubar.quit'Select'
 Assert 0=↑##.FilesAndDirs.RmDir folder

 targetFolder←##.FilesAndDirs.GetTempSubDir''
 folder←('outputpath=''',targetFolder,'''')CreateFolderAndMarkdownFile msg
 n←∆RunMeddy folder,'/level2/my.md'
 n.Markdown.Text,←⊂'Changed at ',##.APLTreeUtils2.FormatDateTime ⎕TS
 n.∆parms.firstFlag←0
 1 ⎕NQ n.∆Menubar.save'Select'
 →T.GoToTidyUp 0≠≢↑##.FilesAndDirs.Dir folder,'\*.html'
 →T.GoToTidyUp 1≠≢↑##.FilesAndDirs.Dir targetFolder,'\*.html'

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ 1 ⎕NQ n.∆Menubar.quit'Select' ⋄ :EndTrap
 :Trap 0 ⋄ ##.FilesAndDirs.RmDir folder ⋄ :EndTrap
 :Trap 0 ⋄ ##.FilesAndDirs.RmDir targetFolder ⋄ :EndTrap
