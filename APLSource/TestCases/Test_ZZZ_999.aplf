 R←Test_ZZZ_999(stopFlag batchFlag);⎕IO;⎕ML;v;n;d;rf;f1;⎕TRAP
⍝ Check the "Version" function
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 rf←#.Meddy.Meddy

⍝ First we check whether "Version" returns a valid result:
 (n v d)←rf.Version
 v←{⍵↑⍨⌊/¯1+⍵⍳'+-'}v
 d←{⍵/⍨3>+\'-'=⍵}d      ⍝ Remove trailing stuff like "-beta"
 v←{⍵/⍨3>+\'.'=⍵}v      ⍝ Remove build ID
 f1←~5≤⍴v
 f1∨←2≠'.'+.=v
 f1∨←~∧/⎕D∊⍨v~'.'

 f1∨←10≠⍴d
 f1∨←d[5 8]∨.≠'-'
 f1∨←~∧/⎕D∊⍨d~'-'

 :If f1
     ⎕←'Result of the "Version" function is invalid.'
 :EndIf

 →T.FailsIf f1

 R←T._OK
