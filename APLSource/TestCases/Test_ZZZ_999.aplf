 R←Test_ZZZ_999(stopFlag batchFlag);⎕IO;⎕ML;v;n;d;xml;rf;f1;f2;⎕TRAP
⍝ Check the "Version" function and publish.config.
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 rf←#.Meddy.Meddy

⍝ First we check whether "Version" returns a valid result:
 (n v d)←rf.Version
 d←{⍵/⍨3>+\'-'=⍵}d      ⍝ Remove trailing stuff like "-beta"
 v←{⍵/⍨3>+\'.'=⍵}v      ⍝ Remove build ID
 f1←~5≤⍴v
 f1∨←2≠'.'+.=v
 f1∨←~∧/⎕D∊⍨v~'.'

 f1∨←10≠⍴d
 f1∨←d[5 8]∨.≠'-'
 f1∨←~∧/⎕D∊⍨d~'-'

⍝ publish.config must be in line with what "Version" returns of course:
 xml←⎕XML⊃⎕NGET'publish.config'
 f2←v≢3⊃xml[xml[;2]⍳⊂'version';]
 f2∨←d≢3⊃xml[xml[;2]⍳⊂'date';]

 :If f1
     ⎕←'Result of the "Version" function is invalid.'
 :EndIf
 :If f2
     ⎕←'The contents of "publish.config" is not in line with "Version".'
 :EndIf

 →T.FailsIf f1∨f2

 R←T._OK
