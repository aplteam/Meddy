 R←Test_ZZZ_998(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;report;buff;parms;ref
⍝ Checks on two text vectors: "⍝TODO⍝" and "⍝CHECK⍝"; never fails, just reports.
 ⎕IO←0 ⋄ ⎕ML←3
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 ref←#.Meddy.Meddy

 R←T._OK
 report←''

 :If 0=⎕SE.⎕NC'_Fire'
     ⎕SE.UCMD'Fire -noGUI'
 :EndIf
 :If 0=⎕SE.⎕NC'_Fire'
     R←T._NotApplicable
 :Else
     parms←⎕SE._Fire.Fire.API.CreateSearchParms
     parms.StartSearchIn←ref
     buff←parms ⎕SE._Fire.Fire.API.Search'⍝CHECK⍝'
     buff←(2<⌊buff[;2])/buff[;0]            ⍝ Only functions, operators, classes, interfaces and namespace scripts
     buff←(buff≢¨⎕XSI[0])/buff              ⍝ remove caller
     buff←(0<∊⎕NC¨buff)⌿buff                ⍝ remove local vars
     :If 0≠≢buff
         report,←↓(⊂' ⍝CHECK⍝ found:'),(⊂'   '),¨buff
     :EndIf
     buff←parms ⎕SE._Fire.Fire.API.Search'⍝TODO⍝'
     buff←(2<⌊buff[;2])/buff[;0]            ⍝ Only functions, operators, classes, interfaces and namespace scripts
     buff←(buff≢¨⎕XSI[0])/buff              ⍝ remove caller
     buff←(0<∊⎕NC¨buff)⌿buff                ⍝ remove local vars
     :If 0≠≢buff
         report,←↓(⊂' ⍝TODO⍝ found:'),(⊂'   '),¨buff
     :EndIf
     :If 0=≢report
         report,←⊂'    Nothing found!'
     :Else
         ⎕←'Checking for ⍝TODO⍝ and ⍝CHECK⍝ '{⍵↑,'--- ',⍺,' ',⍵⍴'-'}80⌊⎕PW-1
         ⎕←⊃report
     :EndIf
 :EndIf
⍝Done
