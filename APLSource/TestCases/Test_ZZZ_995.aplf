 R←Test_ZZZ_995(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;bridgeDLL;bridgeVersion;tno;size;dyalognet;dyalognetVersion
⍝ Checks whether the .NET bridge DLLs (bridge*.dll and dyalognet.dll) are in line.
 ⎕IO←0 ⋄ ⎕ML←3
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T._OK

 bridgeDLL←↑##.FilesAndDirs.Dir'bridge*.dll'
 Assert 1=≢bridgeDLL
 bridgeDLL←↑bridgeDLL
 bridgeVersion←{⍵↑⍨+/∧\⍵∊⎕D}'bridge'{(≢⍺)↓⍵}bridgeDLL
 tno←'dyalognet.dll'⎕NTIE 0
 size←⎕NSIZE tno
 dyalognet←⎕NREAD tno 160 size 0
 ⎕NUNTIE tno
 dyalognetVersion←{⍵{⍺↑⍨-+/∧\⍵∊⎕D,'.'}⌽⍵}{⍵{⍵[1]↑⍵[0]↓⍺}↑'FileVersion\W+([0-9]+\.[0-9]+\.[0-9]+)'⎕S 0 1⍠('Mode' 'D')⊣⍵}dyalognet
 →T.PassesIf bridgeVersion≡{'.'~⍨⍵/⍨2>+\⍵='.'}dyalognetVersion

⍝Done
